plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '7.0.0'
    id 'maven-publish'
    id 'java-library'
    id 'signing'
}

group = webOsGroup
version = webOsVersion

repositories {
    maven { url "https://maven.aliyun.com/nexus/content/groups/public" }
    mavenCentral()
}

dependencies {
    implementation("org.springframework.boot:spring-boot-loader:${SpringBootVersion}")
    implementation project(":os-api")
    compileOnly("org.springframework.boot:spring-boot-starter-web:${SpringBootVersion}")
    implementation "javax.servlet:javax.servlet-api:4.0.1"
    implementation "jakarta.websocket:jakarta.websocket-api:1.1.2"
//    implementation files("D:\\test\\web-os2\\os-core\\build\\libs\\os-core-0.0.1-SNAPSHOT.jar")
}
jar {
    enabled = false
    dependsOn(shadowJar { archiveClassifier = null })
}
shadowJar {
    manifest {
        attributes 'Main-Class': 'cn.donting.web.os.launch.WebosLauncher'
    }
}
// 打包sourcesJar任务
task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

// 打包javadocJar任务
task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier = 'javadoc'
    from javadoc.destinationDir
}

tasks.withType(Javadoc) {
    failOnError false
    options.addStringOption('Xdoclint:none', '-quiet')
    options.addStringOption('encoding', 'UTF-8')
    options.addStringOption('charSet', 'UTF-8')
}

publishing {
    repositories {
        maven {
            File mv = new File(getProject().getRootDir().getPath() + File.separator + "maven.properties")
            if(!mv.exists()){
                return
            }
            Properties properties = new Properties()
            properties.load(new FileInputStream(mv))
            def userName = properties.getProperty("username")
            def passWord = properties.getProperty("password")
            def releasesRepoUrl = "https://oss.sonatype.org/content/repositories/releases/"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
            credentials {
                username userName
                password passWord
            }
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
        }
    }
    publications {
        mavenJava(MavenPublication) {
            groupId = webOsGroup
            artifactId = "web-os-launch"
            version = webOsVersion
//            from components.java
            artifact shadowJar
            artifact sourcesJar
            artifact javadocJar
            pom {
                name = 'web-os-launch'
                description = 'os-launch'
                url = 'https://github.com/WebOSCrud/web-os'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'donting'
                        name = 'donting'
                        email = '596643891@qq.com'
                    }
                }

            }
        }
    }
}



test {
    useJUnitPlatform()
}